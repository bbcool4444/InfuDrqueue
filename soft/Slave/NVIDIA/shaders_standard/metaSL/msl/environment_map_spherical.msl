/*****************************************************************************
 * Copyright 1986-2011 by mental images GmbH, Fasanenstr. 81, D-10623 Berlin,
 * Germany. All rights reserved.
 *****************************************************************************/
shader Environment_map_spherical
{
  input:
    Texture2d env_tex = "default_reflection_spherical.dds"
	{
		display_name("Environment Texture");
		description("The environment texture from which the colors will be sampled.");
	};
    float intensity = 1.0
   	{		
		display_name("Intensity");
		soft_range(0.0,10.0);
		description( "How brightly the environment map will be reflected in "
					 "the scene." );
	};
    
  output:
    Color result;
    
    void main()
    {
		float3 v = transform_vector("internal", "world", direction);
		
		float theta, gamma;
	
		gamma = asin(v.z);
		theta = atan2(v.x, v.y);

		float2 texture;
		texture.x = (theta/PI);
		texture.y = (gamma/(PI * 0.5));
			
		texture = (texture + 1.0) * 0.5;
			
		result  = tex2D(env_tex, texture);
		result *= intensity;
    }
} 
{ 
	shader_type( "environment" ); 
	description( "Uses the spherical mapping to map the texture to "
	             "environment reflection directions." );
}
;
